apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 34
    namespace 'com.bbl.module_ads'
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        productFlavors {
        }
        buildTypes {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    // Task để tạo JavaDoc
    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        
        // Exclude non-Java files
        exclude '**/billing/**'
        options {
            failOnError false
            quiet()
        }
    }
    
    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier.set('javadoc')
        from androidJavadocs.destinationDir
    }
    
    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }
    
    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime:2.0.0"

    // Admob
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'

    implementation "com.android.billingclient:billing:7.0.0"
    implementation "com.google.guava:guava:27.1-android"

    implementation 'com.google.firebase:firebase-analytics:22.0.2'
    implementation 'com.google.firebase:firebase-config:23.0.1'
    implementation 'com.google.code.gson:gson:2.8.9'

    // Mediation
    implementation 'com.google.ads.mediation:facebook:6.17.0.0'
    implementation 'com.google.ads.mediation:applovin:12.5.0.0'
    implementation 'com.google.ads.mediation:vungle:7.3.2.0'
    implementation 'com.google.ads.mediation:pangle:6.0.0.3.0'
    implementation 'com.google.ads.mediation:mintegral:16.7.71.0'
    implementation 'com.unity3d.ads:unity-ads:4.12.2'
    implementation 'com.google.ads.mediation:unity:4.12.2.0'
    implementation 'com.google.ads.mediation:ironsource:8.2.1.0'

    // Facebook SDK
    implementation 'com.facebook.android:facebook-android-sdk:16.1.3'

    // Adjust
    implementation 'com.adjust.sdk:adjust-android:4.33.1'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // Lottie
    implementation 'com.airbnb.android:lottie:6.3.0'

    // Sdp & Ssp
    implementation 'com.intuit.sdp:sdp-android:1.1.1'
    implementation 'com.intuit.ssp:ssp-android:1.1.0'
    implementation 'com.google.firebase:firebase-config:23.0.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                
                groupId = 'com.github.Binary-Bridge-Labs'
                artifactId = 'LibAds-Tiktok'
                version = '1.0.0'
                
                artifact(androidSourcesJar)
                artifact(androidJavadocsJar)
                
                pom {
                    name = 'LibAds-Tiktok'
                    description = 'Android Ads Library with TikTok Ads Integration and Adjust SDK'
                    url = 'https://github.com/Binary-Bridge-Labs/LibAds-With-Tiktok'
                    
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'chinhptit891'
                            name = 'chinhptit891'
                            email = 'chinhptit891@github.com'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:https://github.com/Binary-Bridge-Labs/LibAds-With-Tiktok.git'
                        developerConnection = 'scm:git:ssh://github.com/Binary-Bridge-Labs/LibAds-With-Tiktok.git'
                        url = 'https://github.com/Binary-Bridge-Labs/LibAds-With-Tiktok'
                    }
                }
            }
        }
        
        repositories {
            maven {
                name = 'GitHubPackages'
                url = 'https://maven.pkg.github.com/Binary-Bridge-Labs/LibAds-With-Tiktok'
                credentials {
                    username = System.getenv('GITHUB_USERNAME') ?: 'chinhptit891'
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }
}
